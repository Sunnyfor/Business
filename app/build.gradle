apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

/* ============================= 定义方法 =========================== */

def static appName() {
    return "51Business"
}

static int appVersionCode() {
    return 7
}

static String appVersionName() {
    return "1.4.0"
}

def static buildTime() {
    return new Date().format("yyyyMMdd")
}

/* ================================================================== */
android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.zuoyu.business"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode appVersionCode()
        versionName appVersionName()
        flavorDimensions appVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /* 配置多渠道号 */
    productFlavors {

        // 1. 360手机助手
        // 2. 百度手机助手
        // 3. 应用宝
        // 4. h5
        // 5. 官网

        qh360 {}
        baidu {}
        yingyongbao {}
        h5 {}
        park51 {}


    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    /* 修改打包后的apk名称 */
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                if (outputFileName.endsWith('.apk')) {
                    outputFileName = "${appName()}_v${variant.versionName}_${variant.flavorName}_${buildTime()}.apk"
                }
            }
    }

//    dexOptions {
//        dexInProcess true
//        preDexLibraries true
//        javaMaxHeapSize "4g"//越大越好
//        incremental true
//    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'

    // 百分比布局适配
    compile 'com.zhy:autolayout:1.4.5'

    // 网络请求
    compile 'com.zhy:okhttputils:2.6.2'

    // 解析数据
    compile 'com.alibaba:fastjson:1.2.17'

    // 图片加载框架
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // 算法加密
    compile files('libs/bcprov-jdk15-143.jar')
    compile files('libs/zxing.jar')

    // 沉浸式状态栏工具类（源码已拷贝到项目里）
    compile 'com.jaeger.statusbaruitl:library:1.3.2'
    // (包括SnackBar工具类)
    compile 'com.android.support:design:26.1.0'

    // 图片支持缩放移动（仿微信朋友圈）
    compile 'com.uwetrottmann.photoview:library:1.2.4'

    //权限管理库
    compile 'com.yanzhenjie:permission:1.1.2'


}
repositories {
    mavenCentral()
    google()
}
